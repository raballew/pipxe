#!ipxe
# https://ipxe.org/docs
# https://ipxe.org/settings

# Show the system information.
# NB you can run "config" to interactively see all the possible variables.
echo Platform..... ${platform}
echo Architecture. ${buildarch}
echo Product...... ${smbios/product}
echo Manufacturer. ${smbios/manufacturer}
echo Asset........ ${smbios/asset}
echo UUID......... ${smbios/uuid}
echo Serial....... ${smbios/serial}

echo Configuring net0 ${net0/mac} (${net0/chip}) from dhcp...
:retry_ifconf
ifconf --configurator dhcp net0 || goto retry_ifconf
echo MAC address.. ${net0/mac}
echo IP address... ${net0/ip}
echo Subnet mask.. ${net0/netmask}
echo Gateway...... ${net0/gateway}
echo DNS.......... ${net0/dns}
echo Hostname..... ${hostname}
echo Domain....... ${domain}
echo Next Server.. ${next-server}
echo Filename..... ${filename}

kernel http://${next-server}/fedora-coreos-live-kernel-aarch64 initrd=main coreos.live.rootfs_url=http://${next-server}/fedora-coreos-live-rootfs.aarch64.img ignition.firstboot ignition.platform.id=metal systemd.unified_cgroup_hierarchy=0
initrd --name main http://${next-server}/fedora-coreos-live-initramfs.aarch64.img
boot